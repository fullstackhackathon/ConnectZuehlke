apply plugin: 'com.moowork.node'

def clientRoot = file("${project.rootDir}/frontend")
def clientDist = file("${clientRoot.getAbsolutePath()}/dist")

node {
    version = '10.13.0'
    download = true
    // Set the work directory for unpacking node
    workDir = new File(clientRoot, ".gradle/nodejs")
    // Set the work directory where node_modules should be located
    nodeModulesDir = clientRoot
}

task clientBuild(type: NpmTask) {
    group = 'Client'

    description = 'Builds the client'
    workingDir = clientRoot

    args = ['run', 'build']
    inputs.files fileTree(dir: clientRoot, excludes: ["dist/**", "test/**"])
    outputs.dir(clientDist)
}

task clientArtifacts(type: Copy) {
    from clientDist
    into "build/classes/static"
}

task testClientCi(type: NpmTask){
    group = 'Client'

    description = 'Run the client tests in HeadlessChrome'
    workingDir = clientRoot

    args = ['run', 'ci-test']
}

task testE2EClientCi(type: NpmTask){
    group = 'Client'

    description = 'Run the E2E tests in HeadlessChrome'
    workingDir = clientRoot

    args = ['run', 'ci-e2e']
}

task lintClient(type: NpmTask){
    group = 'Client'
    
    description = 'Run the client linting process'
    workingDir = clientRoot

    args = ['run', 'lint']
}

npmInstall.dependsOn npmSetup
clientBuild.dependsOn npmInstall
clientArtifacts.dependsOn clientBuild

testClientCi.dependsOn npmSetup
testE2EClientCi.dependsOn npmSetup
lintClient.dependsOn npmSetup